dnl Process this file with autoconf to produce a configure script.
AC_INIT(curspriv.h)
AC_CONFIG_HEADER(config.h)

dnl Checks for system first
AC_CANONICAL_SYSTEM
case "$target" in
	*hp-hpux*) 
		SYS_DEFS="-D_HPUX_SOURCE"
		;;
	rs6000-ibm-aix*) 
		SYS_DEFS="-D_ALL_SOURCE"
		;;
	*dec-osf*)
		;;
	sparc*solaris*)
		;;
	sparc*sunos*)
		;;
	*linux*)
		;;
esac
AC_SUBST(SYS_DEFS)

dnl Checks for one of the C compilers below - change if yours not there.
CC_LIST="c89 acc gcc cc"
MH_PROG_CC

dnl Check for other programs.
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl ensure that the system has System V IPC support
MH_IPC

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h time.h sys/time.h memory.h stdarg.h sys/select.h unistd.h errno.h)
MH_HAVE_PROTO


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_ISC_POSIX
dnl AC_CHECK_TYPE(fd_set,int)

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(select memmove vsscanf usleep poll)

dnl Check for X includes and X libraries
MH_CHECK_X_INC
MH_CHECK_X_LIB
MH_CHECK_X_HEADERS(DECkeysym.h Sunkeysym.h)
MH_CHECK_X_KEYDEFS(XK_KP_Delete XK_KP_Insert XK_KP_End XK_KP_Down XK_KP_Next \
		XK_KP_Left XK_KP_Right XK_KP_Home XK_KP_Up XK_KP_Prior)

dnl allow --enable-debug to compile in debug mode
AC_ARG_ENABLE(debug,
	[  --enable-debug          turn on debugging],
	[with_debug=$enableval],
	[with_debug=no],
)
cflags_g="`echo $CFLAGS | grep -c '\-g'`"
cflags_O="`echo $CFLAGS | grep -c '\-O'`"

if test "$with_debug" = yes; then
	if test "$cflags_g" = "0"; then
		CFLAGS="${CFLAGS} -g"
	fi
	if test "$cflags_O" != "0"; then
		CFLAGS="`echo ${CFLAGS} | sed -e s/-O//`"
	fi
	CFLAGS="${CFLAGS} -DPDCDEBUG"
else
	if test "$cflags_O" = "0"; then
		CFLAGS="${CFLAGS} -O"
	fi
	if test "$cflags_g" != "0"; then
		CFLAGS="`echo ${CFLAGS} | sed -e s/-g//`"
	fi
fi

AC_OUTPUT(Makefile pdcurses/Makefile demos/Makefile panel/Makefile tools/Makefile saa/Makefile)
